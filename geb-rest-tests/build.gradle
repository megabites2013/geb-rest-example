apply plugin: 'groovy'

repositories {
    mavenCentral()
}

ext.drivers = ["firefox", "chrome", "ie"]

dependencies {
    def seleniumVersion = "2.32.0"
    def gebVersion = "0.9.0-RC-3"


    groovy 'org.codehaus.groovy:groovy:2.1.3'
    compile "org.spockframework:spock-core:0.7-groovy-2.0"
    compile "org.gebish:geb-spock:$gebVersion"
    compile "org.codehaus.groovy.modules.http-builder:http-builder:0.6"

    drivers.each { driver ->
        compile "org.seleniumhq.selenium:selenium-$driver-driver:$seleniumVersion"
    }

    compile "org.seleniumhq.selenium:selenium-support:$seleniumVersion"

}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        testReportDir = reporting.file("$name/tests")
        testResultsDir = file("$buildDir/test-results/$name")

        systemProperty "geb.build.reportsDir", reporting.file("$name/geb")
        systemProperty "geb.env", driver

        // If you wanted to set the baseUrl in your buildâ€¦
        //systemProperty "geb.build.baseUrl", "http://myapp.com"
    }
}

test {
    dependsOn drivers.collect { tasks["${it}Test"] }
    enabled = false
}


